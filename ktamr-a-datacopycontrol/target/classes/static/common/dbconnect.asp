<%
' DBConnect
' 目的 : 集成数据库连接以及一些公用的函数,减少编程量,调整结构
'
' Sub DirectExecute(sql)
' Function OpenSQL(sql)
' Sub GenOptionValues(sql)
' Sub GenOptionSelected(sql,id)
' Sub GenSelect(strsql, strname, nsize)
'
' 使用
'
' 1. 连接数据库
' Set db = New DBConnect
'
' 2. 打开数据集
' Set r = db.OpenSQL "select * from a"
' If r.Eof Then ....
'
' 3. 执行sql语句
' db.DirectExecute sql
'
' 4. 生成select中的option(下拉式选择菜单)
' db.GenOptionValues sql
' db.GenOptionSelected sql,id
'
' 5. 生成select
' db.GenSelect strsql, strname, nsize
Sub WriteDBErr(byval s)
	If Err.Number <> 0 Then
	   response.write "Error # " & CStr(Err.Number) & " was generated by " & Err.Source & "<br>" & Err.Description & "<br>"
	   response.write s & "<br>"
	   response.End
	End If
End Sub

Class DBConnect

Public m_c, m_r

	Private Sub Class_Initialize()
		Set m_c = Server.Createobject("ADODB.CONNECTION")
		m_c.Open Application("ConnectionString")
	End Sub

	Private Sub Class_Terminate()
		Set m_r = Nothing
		m_c.close
		Set m_c = Nothing
	End Sub

	Public Sub SetConnectString(s)
		Set m_c = Nothing
		Set m_c = Server.Createobject("ADODB.CONNECTION")
		On Error Resume Next
		m_c.Open s
		WriteDBErr s
	End Sub

	Public Sub BeginTrans()
		m_c.BeginTrans
	End Sub

	Public Sub EndTrans(ByVal bisOk)
		If bIsOk Then
			m_c.CommitTrans
		Else
			m_c.RollbackTrans
		End If
	End Sub

	Public Sub DirectExecute(sql)
		On Error Resume Next
		m_c.Execute sql
		WriteDBErr sql
	End Sub

	Public Function OpenSQL(byval sql)
		On Error Resume Next
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		m_r.Open sql, m_c, 1, 1
		WriteDBErr sql
		Set OpenSQL = m_r
		Set m_r = Nothing
	End Function

	public function geta(byval sql)
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		
		On Error Resume Next
		m_r.Open sql, m_c
		WriteDBErr sql
		'response.Write "r = (" & m_r(0) & ")<br>"
		If m_r.EOF Then
			geta = ""
		Else
			If IsNull(m_r(0)) Then
				geta = ""
			Else
				geta = CStr(m_r(0))
			End If
		End If
		m_r.Close
		Set m_r = Nothing
	end function
	
	public function getaInt(byval sql)
		Dim r
		r = geta(sql)
		If r = "" Or Not IsNumeric(r) Then
			getaInt = 0
		Else
			getaInt = Cint(r)
		End If			
	end function

	'新加方法 function geta1(byval sql)  09 8.27
	'为了获取到state 和processing的值
	public function geta1(byval sql)
		set m_r=nothing
		set m_r = server.CreateObject("ADODB.RecordSet")
		
		On Error Resume Next
		m_r.open sql,m_c
		WriteDBErr sql
		if m_r.EOF then
			geta1=""
		else
			if isnull(m_r(0)) then
				geta1=""
			else
				cc=cstr(m_r(0))&":"&m_r(1)
			    geta1=cc
			end if
		end if
		
		m_r(0).close
		set m_r=nothing
		
	end function

	public Function getary(sql,n)
		dim getstr
		getstr=""
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		On Error Resume Next
		m_r.Open sql, m_c
		WriteDBErr sql
		If m_r.EOF Then
			getary = ""
		Else
			For i=0 To n
				If getstr<>"" Then
					getstr=getstr&","
				End If
				getstr=getstr&m_r(i)
                'response.Write m_r(i)
			Next
		End If
		getary=getstr
		m_r.Close
		Set m_r = Nothing
	End Function
    
    public Function getArms(sql)
		dim getstr
		getstr=""
		Set m_r = Nothing
		Set m_r = Server.Createobject("ADODB.RecordSet")
		On Error Resume Next
		m_r.Open sql, m_c
		WriteDBErr sql
		If m_r.EOF Then
			getArms = ""
		Else
			do while m_r(0)<>""
                if m_r(0)="" Then
                    exit do
                End if
				If getstr<>"" Then
					getstr=getstr&","
				End If
				getstr=getstr&m_r(0)
                'response.Write m_r(0)
                m_r.movenext
			loop
		End If
		getArms=getstr
		m_r.Close
		Set m_r = Nothing
	End Function
    
    public Function getArmsArray(sql)
		dim getArray(), i
        i=0
		Set m_r = Nothing
		Set m_r = Server.Createobject("ADODB.RecordSet")
		On Error Resume Next
		m_r.Open sql, m_c
		WriteDBErr sql
		If m_r.EOF Then
			reDim Preserve getArray(i)
			getArmsArray = getArray
			Exit Function
		Else
			do while m_r(0)<>""
                if m_r(0)="" Then
                    exit do
                End if
                '加了Preserve只能修改数组最后维的大小
                reDim Preserve getArray(i)
				getArray(i)=m_r(0)
                'response.Write m_r(0)& vbcrlf
                'response.Write getArray(i)& vbcrlf
                i = i+1
                m_r.movenext
			loop
		End If
		getArmsArray=getArray
		m_r.Close
		Set m_r = Nothing
	End Function

	Public Function getRowsArray(sql, splitChar)
		Dim rowsArray(), i, j, rowData
		j = 0
		Set m_r = Nothing
		Set m_r = Server.Createobject("ADODB.RecordSet")
		On Error Resume Next
		m_r.Open sql, m_c, 1, 1
		WriteDBErr sql
		If m_r.EOF Then
			reDim Preserve rowsArray(j)
			getRowsArray = rowsArray
			Exit Function
		Else
			do while m_r.eof = false
				rowData = ""
				for i=0 to m_r.Fields.Count-1
					rowData = rowData & m_r.Fields(i).Value & splitChar
				Next
				'加了Preserve只能修改数组最后维的大小
				rowData=left(rowData,InStrRev(rowData,splitChar)-1)
				reDim Preserve rowsArray(j)
				rowsArray(j)=rowData
				j = j+1
				m_r.movenext
			loop
		End If
		getRowsArray=rowsArray
		m_r.Close
		Set m_r = Nothing
	End Function
	
	public Function getDataToJSON(sql)
		dim rows
		rows=""
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		On Error Resume Next
		m_r.Open sql, m_c,1,1
		WriteDBErr sql
		If m_r.EOF Then
			getDataToJSON = ""
		Else
			if m_r.eof=false and m_r.Bof=false then
				'do while m_r.Fields(0).Name<>""
					If m_r.eof = false Then 
						'-------
						oneRecord = "{"
							for i=0 to m_r.Fields.Count-1
							oneRecord=oneRecord &chr(34)& m_r.Fields(i).Name &chr(34)&":"
							oneRecord=oneRecord & chr(34)& m_r.Fields(i).Value &chr(34) &","
							Next
						' delete the last string ","
						oneRecord=left(oneRecord,InStrRev(oneRecord,",")-1)
						oneRecord=oneRecord & "},"
						'-------
						rows=rows & oneRecord
						m_r.MoveNext
					End If
				'loop
				' delete the last string ","
				rows = left(rows,InStrRev(rows,",")-1)
			end if
		End If
		getDataToJSON=rows
		m_r.Close
		Set m_r = Nothing
	End Function
	'*******************************************************

	Public Sub Execute_ByRequestParas(sqlstr, paras)
		Dim cmd, s, i, p
		Set cmd = Server.CreateObject("ADODB.Command")
		Set cmd.ActiveConnection = m_c
		m_c.commandTimeout = 600
		cmd.CommandText = sqlstr

        On Error Resume Next
		s = sqlstr & "<br>"
		s = s & paras & "<br>"
		Dim parmName
		if not isNull(paras) then
			paraArray = split(paras,",",-1)
			for i = 0 to ubound(paraArray)
				' get parameter value from request
				parmName = paraArray(i)
				If request(parmName).count > 0 Then
					paraArray(i) = request(parmName)
				ElseIf parmName = "date" Then
					paraArray(i) = Cstr(date())
				ElseIf parmName = "now" Then
					paraArray(i) = Cstr(now)
				ElseIf left(parmName,7) = "SESSION" Then
					sessionName = right(parmName,len(parmName)-7)
					paraArray(i) = Session(sessionName)
					If sessionName = "userid" Then
						paraArray(i) = "M" & paraArray(i)
					End If
				Else
					paraArray(i) = ""
				End If
				
				Dim l
				If vartype(paraArray(i)) = 8 Then
					l = stringLen(paraArray(i))
				Else
					l = Len(paraArray(i))
				End If
				s = s & parmName & ":" & paraArray(i) & ",[" & CStr(l) & "]<br>"
				Set p  = cmd.CreateParameter(, vartype(paraArray(i)), 1, l, paraArray(i))
				cmd.Parameters.Append p
			next
		end if
		cmd.Execute
		WriteDBErr s 
	End Sub

	Public Sub Execute_ByParas(sqlstr, paras)
		Dim cmd, r, p
		Set cmd = Server.CreateObject("ADODB.Command")
		Set cmd.ActiveConnection = m_c
		m_c.commandTimeout = 600
		cmd.CommandText = sqlstr

		Dim parmName
		if not isNull(paras) then
			paraArray = split(paras,",",-1)
			for i = 0 to ubound(paraArray)
				' get parameter value from request
				Dim l
				If vartype(paraArray(i)) = 8 Then
					l = stringLen(paraArray(i))
				Else
					l = Len(paraArray(i))
				End If
				Set p  = cmd.CreateParameter(, vartype(paraArray(i)), 1, l, paraArray(i))
				cmd.Parameters.Append p
			next
		end if
		cmd.Execute
	End Sub

	' ----------------
	Public Sub GenOptionValues(sql)
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		m_r.Open sql, m_c

		If m_r.Eof Then
			Response.Write "<option selected value='-1'>现无数据可选</option>"
		Else
			Response.Write "<option value=''>未选择</option>"
			While Not m_r.Eof
				Response.Write "<option value='" & CStr(m_r(0)) & "' >"
				Response.Write CStr(m_r(1))
				Response.Write "</option>" & Chr(13) & Chr(10)
				m_r.MoveNext
			WEnd
		End If
		m_r.Close
	End Sub

	Function SearchIds(id, ids)
		Dim i
		if isNull(id) Or isNull(ids) then
			SearchIds = false
			Exit function
		end if
		idArray = split(ids, ",")
		For i=0 to Ubound(idArray)
			if idArray(i) = CStr(id) then
				SearchIds = true
				Exit function
			end if
		Next
		SearchIds = false
	End Function
	Public Sub GenOptionSelected(sql,ids)
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		m_r.Open sql, m_c

		Dim s,sid
		If m_r.Eof Then
			'Response.Write "<option selected value=''></option>"
		Else
			While Not m_r.Eof
				s = m_r(0)
				Response.Write "<option value='" & s & "'"
				If SearchIds(s, ids) Then
					Response.Write " selected>"
				Else
					Response.Write ">"
				End If
				Response.Write CStr(m_r(1))
				Response.Write "</option>" & Chr(13) & Chr(10)
				m_r.MoveNext
			WEnd
		End If
		m_r.Close
	End Sub

	Public Sub GenSelect(strsql, strname, nsize)
	   Response.Write "<select size=" & CStr(nsize) & " name='" & strname & "'>"
	   GenOptionValues strsql
	   Response.Write "</select>"
	End Sub
	Public Sub GenmulOptiondivSelected(sql,ids)
		Set m_r = Nothing
		Set m_r = Server.CreateObject("ADODB.RecordSet")
		m_r.Open sql, m_c,selecttrue

		Dim s,sid
		if ids<>"" then 
			sid = split(CStr(ids),",")
		End If
		If m_r.Eof Then
			Response.Write "<span>现无数据可选</span>"
		Else
			While Not m_r.Eof
				selecttrue=0
				s = CStr(m_r(0))
				Response.Write "<span id='"&s&"' name='"&s&"'" 
				if ids<>"" then 
					for i=0 to ubound(sid)
						If s = sid(i) Then
							Response.Write " class='select14span'"
							selecttrue=1
						End If
					Next 
				End If
				if selecttrue=0 then Response.write  "class='noselect14span'"
				Response.Write " >"
				Response.Write CStr(m_r(1))
				Response.Write "</span>" &  Chr(10)
				m_r.MoveNext
			WEnd
		End If
		m_r.Close
	End Sub

End Class
%>
